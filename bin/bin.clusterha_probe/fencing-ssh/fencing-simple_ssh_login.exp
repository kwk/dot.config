#!/bin/expect
# This script logs into ssh host or an ilo device that has ssh
# enabled and runs a command. Uses "TCL" syntax and language in
# expect script.
#
# Usage:
#   fencing-simple_ssh_login.exp <hostname> <username> <port> <path to key file | password>
#
# pyexpect is independent project that is pure python and does not use "expect".
# https://pexpect.readthedocs.io/en/stable
# https://www.journaldev.com/1405/expect-script-ssh-example-tutorial
# https://www.pantz.org/software/expect/expect_examples_and_tips.html
#
# The only issue is knowing when they give key file or password. For now the
# "spawn" line has to be uncomment for connnecting to ssh with a key file or
# password. Need to have some control flow if $argv 3 is a path to file then key
# file and if not then password.
#
# The other issue was passing "cipher" as a option. For now, it is hard coded.


# Turn on debugging
exp_internal 0

# Time to wait for all the input to be recieved. In fencing scripts this would
# be the "login_timeout".
set timeout 30

# Set some vars from commandline options passed to script.
set hostname [lindex $argv 0]
set username [lindex $argv 1]
set ssh_port [lindex $argv 2]
set ssh_cipher "aes256-cbc"

# The prompts to look for with expect.
set prompt "$username@"
set ilo_prompt "</>hpiLO->"

# Make these use same index for now and to one of the commandline options.
set ssh_key_file [lindex $argv 3]
set password [lindex $argv 3]


# If $argv3 is a path to existing file then assume it is for a key file.
if {[file exists $ssh_key_file] == 1} {
    # send_user - Output that gets sent to stdout. This is used for sending message
    # to the screen as the script runs. It is great for user feedback, banners, and
    # for generating error messages. If not successful then we never print message.
    send_user "INFO: Attempting to login to $hostname with an ssh key.\n"
    # Make an ssh connection using a ssh key.
    spawn  /usr/bin/ssh $username@$hostname -p $ssh_port -c $ssh_cipher -i $ssh_key_file
} else {
    send_user "INFO: Attempting to login to $hostname with a password.\n"
    # Make an ssh connection using a password.
    spawn  /usr/bin/ssh $username@$hostname -p $ssh_port -c $ssh_cipher
}


# If we are prompted for password then enter password. If we get a prompt then
# just hit enter to get another prompt to account for having enter password if
# prompted.
expect {
    timeout { send_user "\nTimeout Error: Failed to find prompt for $hostname.\n"; exit 1 }
    eof { send_user "\nEOF Error: There was a SSH failure for $hostname.\n"; exit 1 }
    $prompt {send "\r"}
    $ilo_prompt {send "\r"}
    "*assword:" {send "$password\r"}
}
# Check to see if we got a prompt and run command.
expect {
    timeout { send_user "\nTimeout Error: Failed to find prompt for $hostname.\n"; exit 1 }
    eof { send_user "\nEOF Error: There was a SSH failure for $hostname.\n"; exit 1 }
    $prompt {send "logger \"The expect script has logged in with ssh.\"\n"}
    $ilo_prompt {send "show /system1/oemhp_power1 oemhp_powerreg\r"}
}
send_user "INFO: Successfully logged into $hostname.\n"

# Exit
expect {
    timeout { send_user "\nTimeout Error: Failed to find prompt for $hostname.\n"; exit 1 }
    eof { send_user "\nEOF Error: There was a SSH failure for $hostname.\n"; exit 1 }
    $prompt {send "exit\n"}
    $ilo_prompt {send "exit\r"}

}
